/*
 * Copyright 2013 Google Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// JavaCC grammar specification for the Soy type expressions.
//
// @author Talin


// =================================================================================================
// Options
// =================================================================================================

options {
  JDK_VERSION = "1.7";
  STATIC = false;
  UNICODE_INPUT = true;
}


// =================================================================================================
// Parser class
// =================================================================================================

PARSER_BEGIN(TypeParser)

package com.google.template.soy.types.parse;

import com.google.common.collect.Maps;
import com.google.template.soy.base.SourceLocation;
import com.google.template.soy.base.internal.LegacyInternalSyntaxException;
import com.google.template.soy.types.SoyType;
import com.google.template.soy.types.SoyTypeRegistry;
import com.google.template.soy.types.aggregate.ListType;
import com.google.template.soy.types.aggregate.MapType;
import com.google.template.soy.types.aggregate.UnionType;
import com.google.template.soy.types.aggregate.RecordType;
import com.google.template.soy.types.primitive.UnknownType;

import java.io.StringReader;
import java.util.ArrayList;
import java.util.Collections;
import java.util.List;
import java.util.Map;

/**
 * This parser's specification is in TypeParser.jj, which is read by JavaCC and transformed
 * into TypeParser.java. To modify this parser, please edit TypeParser.jj. Do not edit
 * TypeParser.java directly.
 *
 * <p> Important: Do not use outside of Soy code (treat as superpackage-private).
 *
 */
public class TypeParser {

  /** Private constant used by some parse rules. */
  private static final List<SoyType> EMPTY_TYPE_ARGS = Collections.emptyList();

  private SoyTypeRegistry typeRegistry;

  private SourceLocation sourceLocation;

  /**
   * Constructor that takes a string input.
   * @param input The input to parse.
   * @param sourceLocation The location.
   * @param typeRegistry The type registry.
   */
  public TypeParser(
      String input,
      SourceLocation sourceLocation,
      SoyTypeRegistry typeRegistry) {
    this(new StringReader(input));
    this.sourceLocation = sourceLocation;
    this.typeRegistry = typeRegistry;
  }

  /**
   * Attempts to parse the input as a type declaration.
   * @throws TokenMgrError When the input has a token error.
   * @throws SoySyntaxException When the input has a parse error.
   */
  public SoyType parseTypeDeclaration() {
    try {
      return TypeDecl();
    } catch (ParseException e) {
      // TODO(lukes): we need to 'offset' the source location, however this is
      // essentially impossible because the TemplateParser/TemplateNodeBuilder
      // has 'pre stripped' whitespace then so anything we do here would be
      // wrong  ... so we don't try :(
      throw LegacyInternalSyntaxException.createWithMetaInfo(
          ParseErrors.formatParseException(e),
          sourceLocation);
    }
  }
}

PARSER_END(TypeParser)


// =================================================================================================
// Tokens
// =================================================================================================


// Delimiters.
TOKEN:
{
  < LANGLE: "<" >
|
  < RANGLE: ">" >
|
  < LBRACKET: "[" >
|
  < RBRACKET: "]" >
|
  < COMMA: "," >
|
  < VBAR: "|" >
|
  < COLON: ":" >
|
  < DOT: "." >
|
  < QMARK: "?" >
}


// Names of built-in generic types.
TOKEN:
{
  < GENERIC_LIST: "list" >
|
  < GENERIC_MAP: "map" >
}


// Qualified type names.
TOKEN:
{
  < IDENT: ["a"-"z","A"-"Z","_"] ( ["a"-"z","A"-"Z","_","0"-"9"] )* >
}


// Skip unmatched whitespace.
SKIP:
{
  < WS: " " | "\t" | "\n" | "\r" | "\r\n" >
}

// Match everything else as an 'unexpected' token to prevent TokenMgrErrors from being thrown and
// instead this will get turned into ParseExceptions which have more debugging information.
TOKEN : {
    < UNEXPECTED_TOKEN : ~[] >
}


// =================================================================================================
// Grammar
// =================================================================================================


/**
 * TypeDecl -> TypeExpr EOF
 */
private SoyType TypeDecl() :
{
  SoyType type;
}
{
  type = TypeExpr()
  <EOF>

  { return type; }
}


/**
 * TypeExpr -> UnionTypeExpr
 */
private SoyType TypeExpr() :
{
  SoyType type;
}
{
  type = UnionTypeExpr()

  { return type; }
}


/**
 * UnionTypeExpr -> Primary ("|" Primary)*
 */
private SoyType UnionTypeExpr() :
{
  SoyType type;
}
{
  { List<SoyType> members = new ArrayList<SoyType>(); }

  type = Primary()
  { members.add(type); }

  (
    <VBAR>
    type = Primary()
    { members.add(type); }
  )*

  { return UnionType.of(members); }
}


/**
 * Primary -> TypeName | ListType | MapType
 */
private SoyType Primary() :
{
  SoyType type;
}
{
  (
    type = TypeName()
  |
    type = UnknownType()
  |
    type = ListType()
  |
    type = MapType()
  |
    type = RecordType()
  )

  { return type; }
}


/**
 * ListType -> "list" "<" TypeExpr ">"
 */
private SoyType ListType() :
{
  SoyType typeArg;
}
{
  <GENERIC_LIST>
  <LANGLE>
  typeArg = TypeExpr()
  <RANGLE>

  { return typeRegistry.getOrCreateListType(typeArg); }
}


/**
 * MapType -> "map" "<" TypeExpr "," TypeExpr ">"
 */
private SoyType MapType() :
{
  SoyType key, value;
}
{
  <GENERIC_MAP>
  <LANGLE>
  key= TypeExpr()
  <COMMA>
  value = TypeExpr()
  <RANGLE>

  { return typeRegistry.getOrCreateMapType(key, value); }
}


/**
 * RecordType -> "[" ( RecordField ( "," RecordField )* )? "]"
 */
private SoyType RecordType() :
{
  Map<String, SoyType> fields = Maps.newHashMap();
}
{
  <LBRACKET>

  (
    RecordField(fields)

    (
      <COMMA>
      RecordField(fields)
    )*

  )?

  <RBRACKET>

  {
   return typeRegistry.getOrCreateRecordType(fields);
  }
}


/**
 * RecordField -> IDENT ":" TypeExpr
 */
private void RecordField(Map<String, SoyType> fields) :
{
  Token fieldName;
  SoyType fieldType;
}
{
  fieldName = <IDENT>
  <COLON>
  fieldType = TypeExpr()

  {
    if (fields.containsKey(fieldName.image)) {
      throw LegacyInternalSyntaxException.createWithMetaInfo(
          "Duplicate field definition for record: " + fieldName.image,
          sourceLocation);
    }
    fields.put(fieldName.image, fieldType);
  }
}


/**
 * TypeName -> DOTTED_IDENT
 */
private SoyType TypeName() :
{
  String ident;
}
{
  { SoyType typeName; }

  ident = DottedIdent()
  {
    typeName = typeRegistry.getType(ident);
    if (typeName == null) {
      // TODO(lukes): typo detection/suggestions would be nice here
      throw LegacyInternalSyntaxException.createWithMetaInfo(
          "Unknown type '" + ident + "'.",
          sourceLocation);
    }
  }

  { return typeName; }
}


/**
 * UnknownType -> QMARK
 */
private SoyType UnknownType() :
{}
{
  <QMARK>
  { return UnknownType.getInstance(); }
}


/**
 * DottedIdent -> <IDENT> ( (<WS>)* "." (<WS>)* <IDENT> )* >
 */
private String DottedIdent() :
{
  StringBuilder sb = new StringBuilder();
  Token ident;
}
{
  ident = <IDENT>
  {
    sb.append(ident.image);
  }

  (
    <DOT>
    ident = <IDENT>
    {
      sb.append('.');
      sb.append(ident.image);
    }
  )*

  { return sb.toString(); }
}
